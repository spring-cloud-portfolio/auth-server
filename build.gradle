plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.doroshenko.serhey'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven {
        credentials {
            username = findProperty('gitHubUsername') ?: System.getenv('GIT_HUB_USR')
            password = findProperty('gitHubPassword') ?: System.getenv('GIT_HUB_PSW')
        }
        url "https://maven.pkg.github.com/spring-cloud-portfolio/core-lib-starter"
    }
}

ext {
    set('authServerVersion', "0.1.0")
    set('springCloudVersion', "2020.0.0")
    set("dockerImageProfile", project.findProperty("dip") ?: 'default')
}

dependencies {
    implementation('com.github.ben-manes.caffeine:caffeine')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.cloud:spring-cloud-starter-config')
    implementation('com.doroshenko.serhey.lib:core-lib-starter:0.0.1-SNAPSHOT')
    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    implementation("org.springframework.security.experimental:spring-security-oauth2-authorization-server:${authServerVersion}")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootBuildImage {
    publish = true
    cleanCache = false
    environment = ["BP_JVM_VERSION": "15.*"]
    imageName = "cloudportfolio/${project.name}-${dockerImageProfile}"
    docker {
        publishRegistry {
            username = findProperty('dockerHubUsername') ?: System.getenv('DOCKER_HUB_USR')
            password = findProperty('dockerHubPassword') ?: System.getenv('DOCKER_HUB_PSW')
        }
    }
}
